apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def MAJOR_VERSION = 1
def MINOR_VERSION = 0
def PATCH_VERSION = 2

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.dancing_koala.whathaveyoubeenupto"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode MAJOR_VERSION * 10000 + MINOR_VERSION * 100 + PATCH_VERSION
        versionName MAJOR_VERSION + "." + MINOR_VERSION + "." + PATCH_VERSION
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "DB_NAME", "\"whybut-debug.db\""
            minifyEnabled false
        }

        release {
            buildConfigField "String", "DB_NAME", "\"whybut-prod.db\""
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'proguard-rules/butterknife.pro',
                    'proguard-rules/greendao.pro',
                    'proguard-rules/rxjava.pro'
        }
    }
}

greendao {
    schemaVersion 1
    generateTests true
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'

    compile 'com.jakewharton:butterknife:8.7.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'

    compile 'com.code-troopers.betterpickers:library:3.1.0'

    compile 'org.greenrobot:greendao:3.2.2'
//    compile 'net.zetetic:android-database-sqlcipher:3.5.7@aar'

    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.json:json:20080701'
}
